import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.21"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group 'com.github.only52607.luakt'
    version '2.6.0'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation 'cglib:cglib:3.3.0'
        testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    }

    repositories {
        maven { url 'https://www.jitpack.io' }
        mavenCentral()
    }

    tasks.register('sourcesJar', Jar) {
        from sourceSets.main.allJava
        archiveClassifier.set('sources')
    }

    tasks.register('javadocJar', Jar) {
        from javadoc
        archiveClassifier.set('javadoc')
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    afterEvaluate {
        publishing {
            publications {
                "$project.name"(MavenPublication) {
                    artifactId project.name
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
            }

            repositories {
                maven {

                }
            }
        }
    }
}

dependencies {
    api project(":luakt-luaj")
    api project(":luakt-core")
    api project(":luakt-extension")
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs = ["-Xcontext-receivers"]
}